// if running on command line, need to pass seed # and population size as command line args

initialize() {
	if (exists("slimgui"))
	{
		defineConstant("outPath", "/Users/jeff/workspace/selection-against-introgression/theory_and_simulations/results/hg1/");
		defineConstant("seed", 0);
		lines = readFile("/Users/jeff/workspace/selection-against-introgression/datasets/human/hg38_chr1_slim_recmap.txt");
		defineConstant("N1", 1000);
	} else
	{	
		defineConstant("outPath", "results/hg1/");
		lines = readFile("/Users/brogroh/selection-against-introgression/datasets/human/hg38_chr1_slim_recmap.txt");
	}
	
	initializeTreeSeq();
	defineConstant("N2", 1);
	defineConstant("s", 0.4);
	defineConstant("alpha", 0.5);

	initializeMutationRate(0);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	

	// number of windows present in rec map
	defineConstant("L", 248842222);
	initializeGenomicElement(g1, 0, L-1);

	// read recombination map for hg1
	rates = NULL;
	ends = NULL;
	
	for (line in lines)
	{	
		components = strsplit(line, "\t");
		ends = c(ends, asInteger(components[0]));
		rates = c(rates, asFloat(components[1]));
	}
	ends = ends - min(ends);
	ends = asInteger(ends) + 1; 
	ends = c(ends[1:(size(ends)-1)] - 2, L); // see SLiM manual section 6.1.2: this converts to zero based indexing and shifts from starts to ends
	
	initializeRecombinationRate(rates, ends);
}

1 {
	sim.addSubpop("p1", N1);
	sim.addSubpop("p2", N2); 
	
	// the donor population carries 100 deleterious loci 
	p2.individuals.genomes.addNewMutation(m1, 0, asInteger(seq(0, L-1, by = L/100))); 

	p1.setMigrationRates(p2,alpha);
	sim.treeSeqRememberIndividuals(sim.subpopulations.individuals);
}

1 late() {p2.setSubpopulationSize(0);}

//calculate fitness
1:1000 late() {
	inds = p1.individuals;
	introgressed = inds.countOfMutationsOfType(m1);
	// an individual homozygous for all 100 deleterious loci experiences a fitness reduction of 0.4
	inds.fitnessScaling = 1.0 - (introgressed/(200))*s;
}


// output: gen 3 late are F2 offspring
3 late() {
	sim.treeSeqRememberIndividuals(sim.subpopulations.individuals); // these are F2s
}
6 late() {
	sim.treeSeqRememberIndividuals(sim.subpopulations.individuals); // these are F5s
}
11 late() {
	sim.treeSeqRememberIndividuals(sim.subpopulations.individuals); // these are F10s
}
51 late() {
	sim.treeSeqRememberIndividuals(sim.subpopulations.individuals); // these are F50s
}
101 late() {
	sim.treeSeqRememberIndividuals(sim.subpopulations.individuals); // these are F100
}
501 late() {
	sim.treeSeqRememberIndividuals(sim.subpopulations.individuals); // these are F500
}
1001 late() {
	sim.treeSeqOutput(outPath + "/N" + N1 + "_replicate" + seed + ".trees"); }

import numpy as np
import os
years = ["2006","2008","2013","2015","2018"]
chrs = [s.replace("LD_map_xbirchmanni-COAC-10x-","").replace(".post.txt_mod.bed","") for s in os.listdir("LD_recMap")]

rule all:
	input:
		expand("ACUA_{year}/anc_rec_cds_varDecomp.RData",year=years),
		expand("ACUA_{year}/anc_rec_cds_corDecomp.RData",year=years),
		expand("ACUA_{year}/anc_rec_cds_MODWT.RData",year=years),
		expand("ACUA_{year}/anc_rec_cds_DWT.RData",year=years),
		expand("ACUA_{year}/chrSignalMeans.RData",year=years),
		expand("ACUA_{year}/totalVar.RData",year=years),
		"dwt_lm_plot_data.txt"
#		expand("ACUA_{year}/avgFrq_totalCor.RData",year=years)

rule interpolate_ancestry_single_chrom:
	input: 
		expand("ancestry-probs-par{par}_allchrs_ACUA_historical_{{year}}.tsv", par=["1","2"]),
		"LD_recMap/LD_map_xbirchmanni-COAC-10x-{chr}.post.txt_mod.bed"
	output:
		"ACUA_{year}/{chr}.RData"
	shell:
		"Rscript --vanilla interpolate_ancestry_single_chrom.R {wildcards.year} {wildcards.chr}"

rule all_year_chr:
	input:
		expand("ACUA_{year}/{chr}.RData",year=years,chr=chrs)

rule make_kb_bed:
	input:
		"xbir10x_chrlengths.txt",
	output:
		"xbir_1kb.bed",
	shell:
		"Rscript --vanilla xbir_makeBed_1kb.R | sort -k1,1 -k2,2n > {output}"

rule make_rec_bed:
	input:
		"xbir10x_chrlengths.txt",
		"cM_lengths_birchmanni10x.txt",
		expand("LD_recMap/LD_map_xbirchmanni-COAC-10x-{chr}.post.txt_mod.bed",chr=chrs)
	output:
		"xbir_LDRecMap.bed"
	shell:
		"Rscript --vanilla xbir_makeBed_LDRecMap.R | sort -k1,1 -k2,2n > {output}"

rule make_CDS_bed:
	input:
		"xiphophorus_birchmanni_10x_12Sep2018_yDAA6.gtf" 
	output:
		"xbir_CDS.bed"
	shell:
		"""
		grep 'CDS' {input} | cut -f1,4,5 | awk '{{print $1 "\t" $2-1 "\t" $3}}' |\\
		sort -k1,1 -k2,2n | bedtools merge -i stdin > {output}
		"""

rule calculate_cm_per_kb: 
	input:
		"xbir_1kb.bed",
		"xbir_LDRecMap.bed"
	output:
		"xbir_1kb_rec.bed"
	shell:
		# This gets the weighted mean of r for 1kb windows, the awk statement converts to cM
		"""
		bedmap --echo --delim "\t" --wmean --sci xbir_1kb.bed xbir_LDRecMap.bed |\\
		awk '{{print $1 "\t" $2 "\t" $3 "\t" ($3-$2)*$4*100}}' > {output}
		"""

rule cds_per_kb:
	input:
		A="xbir_1kb.bed",
		B="xbir_CDS.bed"
	output:
		"xbir_1kb_CDS.bed"
	shell:
		"""
		bedtools coverage -a {input.A} -b {input.B} | cut -f1-3,5 |\\
		sort -k1,1 -k2,2n > {output}
		"""
rule cds_and_cm_per_kb:
	input:
		A="xbir_1kb_CDS.bed",
		B="xbir_1kb_rec.bed"
	output:
		"xbir_1kb_CDS_and_cM.txt"
	shell:
		"Rscript --vanilla merge_cds_and_cm_1kb.R"

rule avgFrq_totalCor:
	input:
		expand("ACUA_{{year}}/{chr}.RData",chr=chrs)
	output:
		"ACUA_{year}/avgFrq_totalCor.RData"
	shell:
		"Rscript --vanilla avgFrq_totalCor.R {wildcards.year}"

rule combine_chrs_and_run_wavelets:
	input:
		expand("ACUA_{{year}}/{chr}.RData",chr=chrs)
	output:
		"ACUA_{year}/anc_rec_cds_varDecomp.RData",
		"ACUA_{year}/anc_rec_cds_corDecomp.RData",
		"ACUA_{year}/anc_rec_cds_MODWT.RData",
		"ACUA_{year}/anc_rec_cds_DWT.RData",
		"ACUA_{year}/chrSignalMeans.RData",
		"ACUA_{year}/totalVar.RData"
	shell:
		"Rscript --vanilla swordtail_wavelets.R {wildcards.year}"

rule lm_analysis:
	input:
		dwt="ACUA_2018/anc_rec_cds_DWT.RData",
		functions="lm_functions.R",
	output:
		"dwt_lm_plot_data.txt"
	threads: 16
	shell:
		"Rscript --vanilla lm_dwt_coeffs.R {input.dwt} {output} {threads}"
